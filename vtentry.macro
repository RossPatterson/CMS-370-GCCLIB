***********************************************************************
* VTENTRY generates a vector table entry for the memory-resident GCC  *
* runtime routines.  It must be called from within the VTABLE macro.  *
*                                                                     *
* Robert O'Hara, Redmond Washington, May 2009.                        *
* Bob Bolch and Adrian Sutherland, July 2020.                         *
* Peter Coghlan, December, 2020.                                      *
***********************************************************************
         MACRO
&NAME    VTENTRY ,
         GBLA  &OFFSET
         GBLC  &CODE
         AIF   ('&CODE' EQ 'STUBS').A1
         DC    V(&NAME)
         AGO   .A2
.A1      ANOP
*
&NAME    DS    0H                 Stub to call the real routine
         ENTRY &NAME
         USING CMSCRAB,R13        Save area contains some extra stuff
         L     R15,GCCCRABA       Get address of C runtime anchor block
         DROP  R13                Finished with CMSCRAB for now
         USING GCCCRAB,R15        Map GCC C runtime anchor block
         L     R15,GCCLIBVT       Get address of GCCLIB vector table
         DROP  R15                Finished with GCCCRAB for now
         L     R15,&OFFSET.(,R15) Load address of required routine
         BR    R15                Branch to the required routine
.A2      ANOP
&OFFSET  SETA  &OFFSET+4          Increment for next call
         MEND
